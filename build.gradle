plugins {
	id "java"
	id "eclipse"
	id "maven-publish"
	id "com.palantir.git-version" version "0.5.3"
	id "de.abas.esdk" version "0.5.1"
	id "com.dorongold.task-tree" version "1.3"
}

repositories {
	mavenLocal()
	jcenter()
	mavenCentral()
	maven { url "http://" + NEXUS_HOST + ":" + NEXUS_PORT + "/nexus/content/repositories/" + NEXUS_NAME }
}

esdk {
	app {
		name = "sparePartsCatalogueApp"
		vendorId = "ag"
		appId = "spare"
		entryPoints = [
				"java:de.abas.custom.spareParts.catalogue.Main"
		]
		shared = true
		infosystems = ["IS.OW1.REPLACEMENTCATALOGUE"]
		tables = ["Replacement"]
		data = ["data.json"]
		keys = []
		enums = ["FILE_FORMAT"]
	}

	abas {
		homeDir = ABAS_HOMEDIR
		clientDir = ABAS_CLIENTDIR
		clientId = ABAS_CLIENTID
		edpHost = EDP_HOST
		edpPort = EDP_PORT.toInteger()
		edpUser = EDP_USER
		edpPassword = EDP_PASSWORD
	}

	nexus {
		nexusHost = NEXUS_HOST
		nexusPort = NEXUS_PORT.toInteger()
		nexusRepoName = NEXUS_NAME
		nexusPassword = NEXUS_PASSWORD
	}

	ssh {
		host = SSH_HOST
		port = SSH_PORT.toInteger()
		user = SSH_USER
		password = SSH_PASSWORD
		key = SSH_KEY
	}
	installType = "SSH"
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

compileJava {
	options.encoding = "UTF-8"
}

configurations { compile.extendsFrom provided }

group = 'de.abas.esdk.app'
def versionString = gitVersion().toString()
if (versionString.contains("-")) {
	version versionString.split("-")[0].substring(1) + "-SNAPSHOT"
} else {
	version versionString.substring(1)
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId 'de.abas.esdk.app'
			artifactId 'abasag-sparePartsCatalogue'
			artifact createAppJar
		}
	}
	repositories {
		if (project.hasProperty("esdkSnapshotURL") && project.hasProperty("esdkReleaseURL")
				&& project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
			if (project.version.endsWith('-SNAPSHOT')) {
				maven {
					url esdkSnapshotURL
					credentials {
						username nexusUser
						password nexusPassword
					}
				}
			} else {
				maven {
					url esdkReleaseURL
					credentials {
						username nexusUser
						password nexusPassword
					}
				}
			}
		}
	}
}

dependencies {
	provided 'de.abas.homedir:log4j:1.0.0'
	testCompile 'junit:junit:4.12'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
}
